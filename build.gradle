buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'idea'

ext {
    onelib = project(":OneLib")
}

version = onelib.mc_version + "-" + mod_version
group = "xieao.theora"
archivesBaseName = "theora"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = onelib.mc_version + "-" + onelib.forge_version
    mappings = onelib.mcp_mappings
    runDir = "run"
    makeObfSourceJar = false
    replaceIn 'Theora.java'
    replace "@VERSION@", project.version
    replace 'required-after:onelib', "required-after:onelib@[${onelib.mc_version}-${onelib.mod_version},)"
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_1.12.2:4.9.2.196"
    compile onelib
}

jar {
    manifest {
        attributes "FMLAT": "theora_at.cfg"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}